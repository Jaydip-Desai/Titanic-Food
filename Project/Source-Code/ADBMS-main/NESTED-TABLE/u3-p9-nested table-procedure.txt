Write a Procedure to Insert, Update and Delete Authors in Nested Table
======================================================================

Step 1: Create an Object Type for Author
----------------------------------------
CREATE OR REPLACE TYPE author_obj AS OBJECT (
    author_id   NUMBER,
    author_name VARCHAR2(100)
);

Step 2: Create a Nested Table Type of Authors
---------------------------------------------
CREATE OR REPLACE TYPE author_nested IS TABLE OF author_obj;

Step 3: Create a Table with Nested Table column
-----------------------------------------------
CREATE TABLE book_authors (
    book_id NUMBER PRIMARY KEY,
    authors author_nested
) NESTED TABLE authors STORE AS authors_tab;

Step 4: Insert sample data
--------------------------
INSERT INTO book_authors 
VALUES (1, author_nested(author_obj(1,'Tushar'), author_obj(2,'Ankesh')));
COMMIT;

Step 5: Procedure to Insert, Update, Delete Authors
---------------------------------------------------
CREATE OR REPLACE PROCEDURE manage_authors(
    p_book_id   IN NUMBER,
    p_action    IN VARCHAR2,
    p_position  IN NUMBER DEFAULT NULL,
    p_author_id IN NUMBER DEFAULT NULL,
    p_name      IN VARCHAR2 DEFAULT NULL
) IS
    v_authors author_nested;
BEGIN
    -- Fetch existing authors
    SELECT authors INTO v_authors 
    FROM book_authors 
    WHERE book_id = p_book_id 
    FOR UPDATE;

    IF p_action = 'INSERT' THEN
        v_authors.EXTEND;
        v_authors(v_authors.LAST) := author_obj(p_author_id, p_name);
    ELSIF p_action = 'UPDATE' THEN
        v_authors(p_position) := author_obj(p_author_id, p_name);
    ELSIF p_action = 'DELETE' THEN
        v_authors.DELETE(p_position);
    END IF;

    -- Save changes
    UPDATE book_authors SET authors = v_authors WHERE book_id = p_book_id;
    COMMIT;
END;
/

Step 6: Test the Procedure
--------------------------

-- Insert new author
EXEC manage_authors(1, 'INSERT', NULL, 3, 'Meera');

-- Update 2nd author
EXEC manage_authors(1, 'UPDATE', 2, 2, 'Ankit');

-- Delete 1st author
EXEC manage_authors(1, 'DELETE', 1);
