Write a Procedure to Insert, Update and Delete Authors in VARRAY
================================================================

Step 1: Create an Object Type for Author
----------------------------------------
CREATE OR REPLACE TYPE author_obj AS OBJECT (
    author_id   NUMBER,
    author_name VARCHAR2(100)
);

Step 2: Create a VARRAY Type of Authors
---------------------------------------
CREATE OR REPLACE TYPE author_varray IS VARRAY(10) OF author_obj;

Step 3: Create a Table with VARRAY column
-----------------------------------------
CREATE TABLE book_authors_varray (
    book_id NUMBER PRIMARY KEY,
    authors author_varray
);

Step 4: Insert sample data
--------------------------
INSERT INTO book_authors_varray
VALUES (1, author_varray(author_obj(1,'Ankesh'), author_obj(2,'parth'), author_obj(3,'Jayesh'));
COMMIT;

Step 5: Procedure to Insert, Update, Delete Authors
---------------------------------------------------
CREATE OR REPLACE PROCEDURE manage_authors_varray(
    p_book_id   IN NUMBER,
    p_action    IN VARCHAR2,
    p_position  IN NUMBER DEFAULT NULL,
    p_author_id IN NUMBER DEFAULT NULL,
    p_name      IN VARCHAR2 DEFAULT NULL
) IS
    v_authors author_varray;
BEGIN

    SELECT authors INTO v_authors
    FROM book_authors_varray
    WHERE book_id = p_book_id
    FOR UPDATE;
    
    IF p_action = 'INSERT' THEN
        v_authors.EXTEND;
        v_authors(v_authors.LAST) := author_obj(p_author_id, p_name);

    ELSIF p_action = 'UPDATE' THEN
        v_authors(p_position) := author_obj(p_author_id, p_name);

    ELSIF p_action = 'DELETE' THEN
        FOR i IN p_position .. v_authors.COUNT - 1 LOOP
            v_authors(i) := v_authors(i+1);
        END LOOP;
        v_authors.TRIM;  -- remove last element
    END IF;

    -- Save back to table
    UPDATE book_authors_varray
    SET authors = v_authors
    WHERE book_id = p_book_id;

    COMMIT;
END;
/


Step 6: Test the Procedure
--------------------------

-- Insert new author
EXEC manage_authors_varray(1, 'INSERT', NULL, 4, 'Jaimin');


-- Update 2nd author
EXEC manage_authors_varray(1, 'UPDATE', 2, 2, 'parth Murabiya');


-- Delete 1st author
EXEC manage_authors_varray(1, 'DELETE', 1);

